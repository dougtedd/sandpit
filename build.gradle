// Top level build definition

// The list of subprojects is kept in settings.gadle

// Global properties
ext {
    javaxServletVersion = "3.1.0"
    jstlApiVersion = "1.2"
    log4jVersion = "2.1"
    slf4jVersion = "1.7.7"
    springVersion = "4.1.2.RELEASE"
    junitVersion = "4.11"
    jettyVersion = "9.2.5.v20141112"
    tilesVersion = "3.0.5"
}


allprojects {
    apply plugin: 'idea'
    task libVersions << {
        task -> println "Library versions for project $task.project.name:"
        println "   Servlet: " + javaxServletVersion
        println "   Log4J: " + log4jVersion
        println "   Slf4J: " + slf4jVersion
        println "   Spring: " + springVersion
        println "   Junit: " + junitVersion
    }
}

// The following configuration applies only to the subprojects and not the top-level project
subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    dependencies {
        // Use Log4J2 with SLF4J - see: http://architects.dzone.com/articles/using-log4j2-slf4j-spring-4
        compile "org.slf4j:log4j-over-slf4j:" + slf4jVersion
        compile "org.apache.logging.log4j:log4j-slf4j-impl:" + log4jVersion
        compile "org.apache.logging.log4j:log4j-core:" + log4jVersion
        testCompile "junit:junit:" + junitVersion
    }

    configurations.all {
        // Exclude Log4J v1 dependencies
        exclude (group: "org.slf4j", module: "slf4j-log4j12")
        exclude (group: "log4j", module: "log4j")
    }
}

